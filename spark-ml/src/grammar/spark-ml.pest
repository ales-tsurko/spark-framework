module = { SOI ~ (statement | "\n")* ~ EOI }

statement        = _{ (
                        assignment 
                        | node 
                        | signal_def
                        | animation_def
                        | func_def
                        | if_expr 
                        | repeat_expr 
                        | ext_resource 
                        | sub_resource 
                        | call 
                      ) 
                      ~ "\n" 
                    }

ext_resource = { "ext" ~ ( STRING | ID ) }

sub_resource = { "sub" ~ node }

assignment   = !{ ID ~ "=" ~ "\n"* ~ expression }

func_def     = !{ "fn" ~ ID ~ func_args ~ body}
    func_args = { "(" ~ (ID ~ ",")* ~ ID ~ ")" }

signal_def    = !{ signal_id ~ "=>" ~ signal_id }
    signal_id = { ID ~ ("." ~ ID )+ }

animation_def        = !{ signal_id ~ "~" ~ animation_params }
    animation_params = { node_id ~ attributes }

expression = _{ node 
                | node_anon 
                | if_expr
                | repeat_expr
                | BOOL
                | algebraic_expr 
                | call
                | bool_expr 
                | list
                | NUMBER 
                | STRING 
                | GD_VALUE 
              }

if_expr    = !{ "if" ~ bool_expr ~ body ~ elseif* ~ ifelse? }
    elseif = { "\n"+ ~ "else if" ~ bool_expr ~ body }
    ifelse = { "\n"+ ~ "else" ~ body }

repeat_expr = !{ "repeat" ~ INT ~ body }

body               = ${ blank ~ PEEK_ALL ~ PUSH(INDENT)
                        ~ (body_statement ~ blank ~ (PEEK_ALL ~ body_statement ~ INDENT? ~ blank)*)? 
                        ~ PEEK_ALL? ~ expression ~ INDENT? ~ DROP
                      }
    blank          = _{ ((INDENT? ~ COMMENT)? ~ "\n")+ }
    body_statement = _{ assignment }

algebraic_expr      = !{ (MATH_PRE_OP ~ (alg_value | nested_alg_expr)) 
                         | (alg_value | nested_alg_expr) ~ (MATH_IN_OP ~ (alg_value | nested_alg_expr))+ }
    nested_alg_expr = _{ "(" ~ algebraic_expr ~ ")" }
    alg_value       = _{ call | NUMBER }

bool_expr            = { (BOOL_PRE_OP? ~ (bool_value | nested_bool_expr) ~ BOOL_IN_OP?)+ }
    nested_bool_expr = _{ "(" ~ bool_expr ~ ")" }
    bool_value       = _{ BOOL | call | comparison }
    comparison       = { comp_value ~ COMP_OP ~ comp_value }
    comp_value       = _{ NUMBER | STRING | call }

call            = !{ (fn_call | ID) ~ list_index* ~ attr_access? }
    fn_call     = { ID ~ "(" ~ "\n"* ~ (expression ~ ("," ~ "\n"* ~ expression ~ "\n"*)*)? ~ ")" }
    list_index  = { "[" ~ (NUMBER | call) ~ "]" }
    attr_access = _{ ("\n"* ~ "." ~ call)+ }

node                  = !{ node_id ~ "from" ~ node_id ~ attributes? }
    node_id           = ${ ID ~ (node_id_inner ~ ID?)? }
        node_id_inner = !{ "{" ~ (call | NUMBER | STRING) ~ "}" }
    attributes        = { child_attr+ }
    parent_attr       = { attribute ~ attributes }
    child_attr        = ${ (COMMENT | "\n")+ ~ PEEK_ALL ~ PUSH(INDENT) ~ (parent_attr | attribute)? ~ DROP }
    attribute         = !{ (KEY ~ ":" ~ expression)
                           | node 
                           | call 
                         }

node_anon = !{ "from" ~ attributes }

list = !{ "[" ~ "\n"* ~ expression  ~ ("\n"* ~ "," ~ "\n"* ~ expression ~ "\n"*)* ~ "]" }

GD_VALUE           = ${ "`" ~ GD_VALUE_INNER ~ "`" }
    GD_VALUE_INNER = { (!(NEWLINE | "`") ~ ANY)* }
STRING             = ${ "\"" ~ STRING_INNER ~ "\"" }
    STRING_INNER   = { (!(NEWLINE | "\"") ~ ANY)* }
BOOL               = { "true" | "false" }
MATH_IN_OP         = _{ PLUS | MINUS | DIV | MUL | MOD }
    PLUS           = { "+" }
    MINUS          = { "-" }
    DIV            = { "/" }
    MUL            = { "*" }
    MOD            = { "%" }
MATH_PRE_OP        = _{ NEG }
    NEG            = { "-" }
BOOL_IN_OP         = _{ AND | OR }
    AND            = { "&&" }
    OR             = { "||" }
BOOL_PRE_OP        = _{ NOT }
    NOT            = { "!" }
COMP_OP            = _{ EQ | NEQ | LT | GT | LT_EQ | GT_EQ }
    EQ             = { "==" }
    NEQ            = { "!=" }
    LT             = { "<" }
    GT             = { ">" }
    LT_EQ          = { "<=" }
    GT_EQ          = { ">=" }
NUMBER             = @{ "-"? ~ INT ~ ("." ~ DIGITS ~ EXP? | EXP)? }
    INT            = @{ "0" | (ASCII_NONZERO_DIGIT ~ DIGITS?) }
    DIGITS         = @{ (ASCII_DIGIT | ("_" ~ ASCII_DIGIT))+ }
    EXP            = @{ ("E" | "e") ~ ("+" | "-")? ~ INT }
KEY                = @{ "@"? ~ ID }
ID                 = @{ (LETTER ~ ("_" | LETTER | NUMBER )*) }
INDENT             = _{ (" " | "\t")+ }
COMMENT            = _{ "#" ~ (!NEWLINE ~ ANY)* }
WHITESPACE         = _{ " " | "\t" }
