module = { SOI ~ (statement | NEWLINE)* ~ EOI }

statement        = _{ (
                        assignment 
                        | node 
                        | func_def
                        | export
                        | if_expr 
                        | repeat_expr 
                        | ext_resource 
                        | sub_resource 
                        | call 
                      ) 
                      ~ NEWLINE 
                    }

ext_resource = { "ext" ~ ( STRING | ID ) }

sub_resource = { "sub" ~ node }

assignment            = !{ (assign_property | assign_ancestor | assign_list_index | ID) ~ "=" ~ NEWLINE* ~ expression }
    assign_list_index = { ID ~ ("[" ~ (NUMBER | call) ~ "]")+ }
    assign_property   = { (assign_ancestor | assign_list_index | ID) ~ "." ~ (assign_property | assign_list_index | ID) }
    assign_ancestor   = { "^" ~ (assign_list_index | ID) }

func_def      = !{ (PUSH(INDENT) ~ func_def_in ~ DROP) | func_def_in }
    func_def_in = _{"fn" ~ ID ~ func_args ~ body}
    func_args = { "(" ~ (ID ~ ("," ~ ID)*)? ~ ")" }

// it's easier to parse this way
export          = _{ export_func | export_var }
    export_func = {"export" ~ func_def}
    export_var  = {"export" ~ assignment}

expression = _{ node 
                | object 
                | if_expr
                | repeat_expr
                | algebraic_expr 
                | bool_expr 
                | BOOL
                | call
                | list
                | NUMBER 
                | STRING 
                | GD_VALUE 
              }

if_expr    = !{ "if" ~ (bool_expr | BOOL | call) ~ body ~ elseif* ~ ifelse? }
    elseif = { NEWLINE+ ~ "else if" ~ (bool_expr | BOOL | call) ~ body }
    ifelse = { NEWLINE+ ~ "else" ~ body }
    
repeat_expr = !{ "repeat" ~ expression ~ body }

body               = ${ blank ~ 
                            PEEK_ALL ~ PUSH(INDENT) ~ 
                                (
                                    ((body_statement ~ blank ~ PEEK_ALL)+ ~ expression) 
                                    | expression
                                ) ~ INDENT? ~ DROP 
                       }
    body_statement = _{ assignment }

algebraic_expr      = !{ (alg_value | nested_alg_expr) ~ (MATH_IN_OP ~ (alg_value | nested_alg_expr))+ }
    nested_alg_expr = _{ MATH_PRE_OP? ~ "(" ~ algebraic_expr ~ ")" }
    alg_value       = _{ MATH_PRE_OP? ~ call | NUMBER }

bool_expr            = !{ (bool_value | nested_bool_expr) ~ (BOOL_IN_OP ~ (bool_value | bool_expr))* }
    nested_bool_expr = _{ BOOL_PRE_OP? ~ "(" ~ bool_expr ~ ")" }
    bool_value       = _{ BOOL_PRE_OP?  ~ (BOOL | comparison | call) }
        comparison   = { comp_value ~ COMP_OP ~ comp_value }
        comp_value   = _{ NUMBER | STRING | BOOL | call }

call              = !{ property_call | ancestor_ref | list_index | fn_call | var_call }
    fn_call       = { ID ~ "(" ~ NEWLINE* ~ (expression ~ ("," ~ NEWLINE* ~ expression ~ NEWLINE*)*)? ~ ")" }
    var_call      = { ID }
    list_index    = { (fn_call | var_call) ~ ("[" ~ (NUMBER | call) ~ "]")+ }
    property_call = { (ancestor_ref | list_index | fn_call | var_call) ~ (NEWLINE* ~ "." ~ (property_call | list_index | fn_call | var_call)) }
    ancestor_ref  = { "^" ~ (list_index | var_call) }

node                  = !{ node_id ~ "from" ~ node_id ~ attributes? }
    node_id           = _{ ID | STRING }
    attributes        = ${ blank ~ PEEK_ALL ~ PUSH(INDENT) ~ attribute ~ (blank ~ PEEK_ALL ~ attribute)* ~ DROP }
    attribute         = !{ (METAKEY | KEY) ~ ":" ~ (attributes | expression) ~ INDENT? }

object           = !{ "{" ~ attributes? ~ properties? ~ blank? ~ "}" }
    properties   = ${ property? ~ blank ~ PEEK_ALL ~ PUSH(INDENT) ~ property ~ (blank ~ PEEK_ALL ~ property)* ~ DROP }
        property = _{ (func_def | assignment) ~ INDENT? }

list = !{ "[" ~ NEWLINE* ~ (expression  ~ (NEWLINE* ~ "," ~ NEWLINE* ~ expression ~ NEWLINE*)*)? ~ NEWLINE* ~ "]" }

blank              = _{ ((INDENT? ~ COMMENT)? ~ NEWLINE)+ }
GD_VALUE           = ${ "`" ~ GD_VALUE_INNER ~ "`" }
    GD_VALUE_INNER = { (!(NEWLINE | "`") ~ ANY)* }
STRING             = ${ "\"" ~ (STRING_INTERP | STRING_INNER)* ~ "\"" }
    STRING_INNER   = { (!(NEWLINE | "\"" | "{") ~ ("\\{" | ANY))+ }
    STRING_INTERP  = !{ "{" ~ expression ~ "}" }
BOOL               = { "true" | "false" }
MATH_IN_OP         = _{ ADD | SUB | DIV | MUL | MOD }
    ADD            = { "+" }
    SUB            = { "-" }
    DIV            = { "/" }
    MUL            = { "*" }
    MOD            = { "%" }
MATH_PRE_OP        = _{ NEG }
    NEG            = { "-" }
BOOL_IN_OP         = _{ AND | OR }
    AND            = { "&&" }
    OR             = { "||" }
BOOL_PRE_OP        = _{ NOT }
    NOT            = { "!" }
COMP_OP            = _{ EQ | NEQ | LT_EQ | GT_EQ | LT | GT }
    EQ             = { "==" }
    NEQ            = { "!=" }
    LT             = { "<" }
    GT             = { ">" }
    LT_EQ          = { "<=" }
    GT_EQ          = { ">=" }
NUMBER             = @{ "-"? ~ INT ~ ("." ~ DIGITS ~ EXP? | EXP)? }
    INT            = @{ "0" | (ASCII_NONZERO_DIGIT ~ DIGITS?) }
    DIGITS         = @{ (ASCII_DIGIT | ("_" ~ ASCII_DIGIT))+ }
    EXP            = @{ ("E" | "e") ~ ("+" | "-")? ~ INT }
METAKEY            = @{ "@" ~ ID }
KEY                = @{ ID }
ID                 = @{ LETTER ~ ("_" | LETTER | NUMBER )* }
INDENT             = _{ (" " | "\t")+ }
COMMENT            = _{ "#" ~ (!NEWLINE ~ ANY)* }
WHITESPACE         = _{ " " | "\t" }
